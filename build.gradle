import groovy.json.JsonSlurper
import org.apache.tools.ant.filters.ReplaceTokens
import java.time.Instant

plugins {
    id "architectury-plugin" version "3.4.160"
    id "dev.architectury.loom" version "1.7.435" apply false
    id "de.undercouch.download" version "4.1.2"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id "base"
}

String default_minecraft_version = "1.16.5"
String minecraft_version = rootProject.properties.containsKey("buildVersion") ? rootProject.getProperties().get("buildVersion") : default_minecraft_version
int minecraft_main_version = minecraft_version.split("\\.")[1] as int
String minecraft_version_int = minecraft_version.split("\\.")[0] +
        minecraft_version.split("\\.")[1].padLeft(2, '0') +
        (minecraft_version.split("\\.").length > 2 ? minecraft_version.split("\\.")[2].padLeft(2, '0') : "00")

boolean is_1_19_3 = (minecraft_version == "1.19.3" || minecraft_version == "1.19.4" || minecraft_main_version >= 20)
boolean is_1_20 = minecraft_main_version >= 20

rootProject.ext.fabric_loader_version = new JsonSlurper().parse(("https://meta.fabricmc.net/v2/versions/loader/" + minecraft_version).toURL())[0]["loader"]["version"]
rootProject.ext.forge_version = minecraft_version + "-" + new JsonSlurper().parse(("https://files.minecraftforge.net/net/minecraftforge/forge/promotions_slim.json").toURL())["promos"][minecraft_version + "-latest"]
rootProject.ext.fabric_api_version = getModrinthVersion("fabric", minecraft_version, "fabric-api")
rootProject.ext.mod_menu_version = getModrinthVersion("fabric", minecraft_version, "modmenu")
rootProject.ext.architectury_version = getModrinthVersion("forge", minecraft_version, "architectury-api").split("\\+")[0]
rootProject.ext.architectury_id = minecraft_main_version == 16 ? "me.shedaniel" : "dev.architectury"

String parchment_version
try {
    parchment_version = is_1_19_3 ? "" : new XmlSlurper().parse("https://ldtteam.jfrog.io/artifactory/parchmentmc-internal/org/parchmentmc/data/parchment-${minecraft_version}/maven-metadata.xml").versioning.release
} catch (Exception ignored) {
    parchment_version = ""
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        annotationProcessor 'systems.manifold:manifold-preprocessor:+'

        minecraft "com.mojang:minecraft:${minecraft_version}"

        mappings(parchment_version == "" ? loom.officialMojangMappings() : loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
        })

        compileOnly files("${rootProject.projectDir}/checkouts/mtr-common.jar")
    }
}

task setupLibrary() {
    doLast {
        String baseURL = is_1_20 ? "https://storage.zbx1425.cn/mtr-artifacts/libs/" : "https://www.minecrafttransitrailway.com/libs/"

        download {
            src "${baseURL}${rootProject.mtr_version}/MTR-common-${minecraft_version}-${rootProject.mtr_version}-dev.jar"
            dest "checkouts/mtr-common.jar"
            overwrite true
        }

        download {
            src "${baseURL}${rootProject.mtr_version}/MTR-fabric-${minecraft_version}-${rootProject.mtr_version}-dev-shadow.jar"
            dest "checkouts/mtr-fabric.jar"
            overwrite true
        }

        download {
            src "${baseURL}${rootProject.mtr_version}/MTR-forge-${minecraft_version}-${rootProject.mtr_version}-dev-shadow.jar"
            dest "checkouts/mtr-forge.jar"
            overwrite true
        }
    }
}

task setupFiles() {
    dependsOn setupLibrary

    doFirst {
    }

    doLast {
        String mappingsZipUrl = "https://github.com/zbx1425/Minecraft-Mappings/archive/refs/heads/1.${minecraft_main_version}${is_1_19_3 && minecraft_main_version < 20 ? ".3" : ""}.zip"

        download {
            src mappingsZipUrl
            dest "common/src/main/java/botamochi129/mtraddontemplate/mappings/files.zip"
            overwrite true
        }

        project.copy {
            from(zipTree("common/src/main/java/botamochi129/mtraddontemplate/mappings/files.zip")) {
                eachFile { file -> file.relativePath = new RelativePath(true, file.relativePath.segments.drop(1) as String[]) }
            }
            into "common/src/main/java/botamochi129/mtraddontemplate/mappings"
            filter(ReplaceTokens, tokens: ["package": "botamochi129.mtraddontemplate.mappings; import mtr.mappings.*"])
        }

        ant.path { ant.fileset(dir: "common/src/main/java/botamochi129/mtraddontemplate/mappings", includes: "Fabric*.java") }.list().each {
            ant.move(file: it, todir: "fabric/src/main/java/botamochi129/mtraddontemplate/mappings")
        }
        ant.path { ant.fileset(dir: "common/src/main/java/botamochi129/mtraddontemplate/mappings", includes: "Forge*.java") }.list().each {
            ant.move(file: it, todir: "forge/src/main/java/botamochi129/mtraddontemplate/mappings")
        }

        ant.delete(dir: "common/src/main/java/botamochi129/mtraddontemplate/mappings")

        project.copy {
            from "checkouts/mtr-fabric.jar"
            into "fabric/run/mods"
        }
        project.copy {
            from "checkouts/mtr-forge.jar"
            into "forge/run/mods"
        }

        project.copy {
            from "common/src/main/BuildConfigTemplate.java"
            into "common/src/main/java/botamochi129/mtraddontemplate"
            filter(ReplaceTokens, tokens: [
                    "version"         : minecraft_version + "-" + rootProject.mod_version,
                    "protocol_version": rootProject.mod_protocol_version ?: "1",
                    "build_time"      : Instant.now().epochSecond.toString()
            ])
            rename "(.+)Template.java", "\$1.java"
        }
    }
}

allprojects {
    apply plugin: "architectury-plugin"

    version = minecraft_version + "-" + project.mod_version
    group = project.maven_group

    repositories {
        maven { url = "https://jitpack.io/" }
        maven { url = "https://maven.terraformersmc.com/" }
        maven { url = "https://maven.parchmentmc.org/" }
        maven { url = "https://maven.shedaniel.me" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        if (minecraft_main_version <= 16) {
            options.release.set(8)
        } else if (minecraft_main_version == 17) {
            options.release.set(16)
        } else {
            options.release.set(17)
        }

        options.compilerArgs += ['-Xplugin:Manifold', '-AMC_VERSION=' + minecraft_version_int]
    }

    afterEvaluate {
        for (def task in it.tasks) {
            if (task != rootProject.tasks.setupFiles && task.name.startsWith("compile")) {
                task.dependsOn rootProject.tasks.setupFiles
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            force "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
        }
    }
}

static def getModrinthVersion(loader, minecraftVersion, projectId) {
    try {
        def versionsArray = new JsonSlurper().parse(("https://api.modrinth.com/v2/project/" + projectId + "/version").toURL())
        for (def versionElement : versionsArray) {
            if (versionElement["loaders"].contains(loader) && versionElement["game_versions"].contains(minecraftVersion)) {
                return versionElement["version_number"]
            }
        }
    } catch (Exception e) {
        return ""
    }
    return ""
}